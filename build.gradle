apply from: "modules_config.gradle"

buildscript {
    ext {
        springBootVersion = "1.3.5.RELEASE"
        scalaVeresion = "2.11.8"
        swaggerVersion = "2.5.0"

        // testing
        mockitoVersion = "1.10.19"

        // db
        queryDslVersion = "3.7.4"
        h2DbVersion = "1.4.192"
        ehCacheVersion = "2.6.11"

        // utils
        guavaVersion = "19.0"
        jacksonJsr310Version = "2.8.0"

        // reports
        jacocoVersion = "0.7.6.201602180812"
        coverallsVersion = "2.4.0"
        scalastyleVersion = "0.8.2"

        // folders
        srcDir = "src/main/scala"
        testDir = "src/test/scala"
        itestDir = "src/itest/scala"
    }
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/release/" }
        maven { url "http://repo.spring.io/libs-snapshot-local" }
        maven { url "http://repo.spring.io/libs-milestone-local" }
        maven { url "http://repo.spring.io/libs-release-local" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:${coverallsVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE"
        classpath "org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:${scalastyleVersion}"
    }
}

subprojects {

    apply plugin: "scala"
    apply plugin: "scalaStyle"
    apply plugin: "com.github.kt3k.coveralls"
    apply plugin: "jacoco"
    apply plugin: "io.spring.dependency-management"
    apply from: "../commons.gradle"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-netflix:1.1.2.RELEASE"
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/release/" }
        maven { url "http://repo.spring.io/libs-snapshot-local" }
        maven { url "http://repo.spring.io/libs-milestone-local" }
        maven { url "http://repo.spring.io/libs-release-local" }
    }

    dependencies {
        // Spring
        compile "org.scala-lang:scala-library:${scalaVeresion}"
        compile("org.springframework.boot:spring-boot-starter-web")
        compile "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-starter-security"
        compile "org.springframework.security.oauth:spring-security-oauth2"
        compile "org.springframework.cloud:spring-cloud-starter-eureka"

        // db
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "com.h2database:h2:${h2DbVersion}"
        compile "net.sf.ehcache:ehcache-core:${ehCacheVersion}"
        compile "com.mysema.querydsl:querydsl-jpa:${queryDslVersion}"
        compile "com.mysema.querydsl:querydsl-apt:${queryDslVersion}:jpa"

        // Swagger
        compile "io.springfox:springfox-swagger2:${swaggerVersion}"
        compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"

        // utils
        compile "com.google.guava:guava:${guavaVersion}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonJsr310Version}"

        // test
        testCompile "org.springframework.boot:spring-boot-starter-test"
        testCompile "org.mockito:mockito-all:${mockitoVersion}"
    }

    scalaStyle {
        configLocation = "scalastyle_config.xml"
        includeTestSourceDirectory = true
        source = srcDir
        testSource = testDir
        testSource += itestDir
    }

    jacoco {
        toolVersion = "${jacocoVersion}"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    tasks.coveralls {
        dependsOn "check"
    }

    configurations {
        itestCompile.extendsFrom testCompile
        itestRuntime.extendsFrom testRuntime
    }

    sourceSets {
        itest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            scala.srcDir file(itestDir)
        }
        test {
            resources.srcDir file('src/itest/resources')
        }
    }

    task itest(type: Test) {
        testClassesDir = sourceSets.itest.output.classesDir
        classpath = sourceSets.itest.runtimeClasspath

        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}